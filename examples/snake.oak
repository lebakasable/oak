#!/usr/bin/env -S sh -c 'stty -icanon -echo; oak run examples/snake.oak; stty sane'

use std.io
use std.libc
use std.math

const ROWS = 10
const COLS = 20

const (
   CELL_EMPTY
   CELL_APPLE
   CELL_SNAKE
)

let cells [ROWS][COLS]int

struct Point {
   x int
   y int
}

fn point_new(x int, y int) Point {
   let point Point
   point.x = x
   point.y = y
   return point
}

const (
   DIR_UP
   DIR_LEFT
   DIR_DOWN
   DIR_RIGHT
)

fn point_step(point *Point, dir int) {
   match dir {
      DIR_UP => point.y -= 1
      DIR_LEFT => point.x -= 1
      DIR_DOWN => point.y += 1
      DIR_RIGHT => point.x += 1
      else => assert false
   }

   point.x = emod(point.x, COLS)
   point.y = emod(point.y, ROWS)
}

const SNAKE_CAP = ROWS*COLS
let snake [SNAKE_CAP]Point
let snake_begin int
let snake_size int

fn snake_add(point Point) {
   if snake_size >= SNAKE_CAP {
      &stdout << "\e[?25h"
      &stdout << "You won!\n"
      exit(0)
   }

   cells[point.y][point.x] = CELL_SNAKE

   snake[(snake_begin + snake_size)%SNAKE_CAP] = point

   snake_size += 1
}

fn snake_del() {
   assert snake_size > 0

   let point = snake[snake_begin]
   cells[point.y][point.x] = CELL_EMPTY

   snake_size -= 1
   snake_begin = (snake_begin + 1)%SNAKE_CAP
}

let head_pos Point
let head_dir int

fn display() {
   for let y = 0, y < ROWS, y += 1 {
      for let x = 0, x < COLS, x += 1 {
         match cells[y][x] {
            CELL_EMPTY => &stdout << "."
            CELL_APPLE => &stdout << "*"
            CELL_SNAKE => &stdout << "#"
            else => assert false
         }
      }
      &stdout << "\n"
   }
}

fn spawn_apple() {
   let x = (rand()<<32)%COLS
   let y = (rand()<<32)%ROWS
   for cells[y][x] != CELL_EMPTY {
      x = (rand()<<32)%COLS
      y = (rand()<<32)%ROWS
   }
   cells[y][x] = CELL_APPLE
}

fn spawn_snake(point Point, dir int, len int) {
   head_pos = point
   head_dir = dir
   snake_add(point)

   for let i = 1, i < len, i += 1 {
      point_step(&head_pos, head_dir)
      snake_add(head_pos)
   }
}

fn main() {
   srand(time(0 as *int))

   fcntl(STDIN, F_SETFL, fcntl(STDIN, F_GETFL, 0) | O_NONBLOCK)
   &stdout << "\e[?25l"

   spawn_snake(point_new(5, 5), DIR_RIGHT, 3)
   spawn_apple()

   let input char
   let pause = false
   let score = 0

   display()
   for true {
      &stdout << "\e[" << ROWS << "A"
      &stdout << "\e[" << COLS << "D"
      display()

      let ret = read(STDIN, &input, 1)
      if ret == 1 {
         match input {
            'z' => if head_dir != DIR_DOWN { head_dir = DIR_UP }
            'q' => if head_dir != DIR_RIGHT { head_dir = DIR_LEFT }
            's' => if head_dir != DIR_UP { head_dir = DIR_DOWN }
            'd' => if head_dir != DIR_LEFT { head_dir = DIR_RIGHT }
            'e' => break
            ' ' => pause = !pause
         }
      } else if ret != EAGAIN {
         &stdout << "\e[?25h"
         &stderr << "error: read command failed\n"
         exit(1)
      }

      if !pause {
         point_step(&head_pos, head_dir)

         match cells[head_pos.y][head_pos.x] {
            CELL_SNAKE => {
               &stdout << "You died with score: " << score << "\n"
               break
            }
            CELL_APPLE => {
               snake_add(head_pos)
               score += 1
               spawn_apple()
            }
            CELL_EMPTY => {
               snake_del()
               snake_add(head_pos)
            }
            else => assert false
         }
      }

      for let i = 0, i < 20000000, i += 1 {}
   }

   &stdout << "\e[?25h"
}
